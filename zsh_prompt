autoload -Uz vcs_info

+vi-git-extra-info() {
	local PROMPT_VCS_EXTRA=''

	if [ -n "$(command git status --porcelain --ignore-submodules -uno 2>/dev/null)" ]; then
		# workdir is dirty
		hook_com[branch]="%F{red}${hook_com[branch]}%f"
	else
		hook_com[branch]="%F{green}${hook_com[branch]}%f"
	fi

	if command git status --porcelain --ignore-submodules 2>/dev/null | grep '^??' &>/dev/null; then
		# untracked files exist
		PROMPT_VCS_EXTRA+='%F{red}*%f'
	fi

	PROMPT_VCS_EXTRA+='%F{yellow}'
	if command git submodule 2>/dev/null | grep -v '^ ' &>/dev/null; then
		PROMPT_VCS_EXTRA+='$'
	fi
	if command git rev-parse --abbrev-ref 'stash@{0}' &>/dev/null; then
		PROMPT_VCS_EXTRA+='+'
	fi
	if command git rev-parse --abbrev-ref @'{u}' &>/dev/null; then
		(( $(command git rev-list --right-only --count HEAD...@'{u}' 2>/dev/null) > 0 )) && PROMPT_VCS_EXTRA+='⇣'
		(( $(command git rev-list --left-only --count HEAD...@'{u}' 2>/dev/null) > 0 )) && PROMPT_VCS_EXTRA+='⇡'
	fi
	PROMPT_VCS_EXTRA+='%f'

	hook_com[misc]="$PROMPT_VCS_EXTRA"
}

+vi-prompt-char() {
	case $vcs in
		git)
			PROMPT_CHAR='±';;
	esac
}

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git*' formats ' %b%m'
zstyle ':vcs_info:git*' actionformats ' %a %b%m'
zstyle ':vcs_info:git*+post-backend:*' hooks git-extra-info
zstyle ':vcs_info:*+pre-get-data:*' hooks prompt-char

setopt prompt_subst

function ei_python {
	if which pyenv &>/dev/null; then
		local pyver="$(pyenv version-name)"
		if [ $pyver != "system" ]; then
			PROMPT_ENV_INFO+="Py${pyver} "
		fi
	fi
}

function ei_ruby {
	if which rbenv &>/dev/null; then
		local pyver="$(rbenv version-name)"
		if [ $pyver != "system" ]; then
			PROMPT_ENV_INFO+="Rb${pyver} "
		fi
	fi
}

typeset -g PROMPT_ENV_INFO
function env_info {
	PROMPT_ENV_INFO=""
	ei_python
	ei_ruby
}

precmd() {
	PROMPT_CHAR='%%'
	vcs_info
	env_info
}

if [[ "$SSH_CONNECTION" != "" ]]; then
	PROMPT_USER="%n@%F{blue}%m%f"
else
	PROMPT_USER="%n"
fi

PROMPT="${PROMPT_USER}:%(5~!%-1~/.../%2~!%~)%(?.. %F{magenta}%?%f) %(!.#.\${PROMPT_CHAR}) "
RPROMPT='${PROMPT_ENV_INFO}${vcs_info_msg_0_}'
